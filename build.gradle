buildscript {
    repositories {
        mavenLocal()
        maven { url "https://repo1.maven.org/maven2" }
    }
    dependencies {
        classpath "org.openapitools:openapi-generator-gradle-plugin:5.0.0"
    }
}

plugins {
    id 'java'
	id 'org.springframework.boot' version '3.4.3'
    id 'org.asciidoctor.convert' version '2.4.0'
    id 'org.openapi.generator' version '7.12.0'
    id("org.unbroken-dome.test-sets") version "4.1.0"
}

apply plugin: 'io.spring.dependency-management'

group = 'com.poss.clash-bot'
version = '1.0.0-SNAPSHOT'
sourceCompatibility = '21'
targetCompatibility = '21'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

ext {
    set('snippetsDir', file("build/generated-snippets"))
}

sourceSets {
    main {
        java.srcDirs += 'build/generated/src/main/java'
    }
}

bootJar {
   archiveBaseName = 'clash-bot-spring-service'
   archiveVersion =  '1.0.0'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb-reactive'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    implementation 'org.springdoc:springdoc-openapi-webflux-ui:1.6.14'
    implementation 'io.swagger.core.v3:swagger-annotations:2.2.8'
    implementation 'javax.validation:validation-api:2.0.1.Final'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.4'
    implementation 'org.mapstruct:mapstruct:1.5.3.Final'
    implementation platform('org.testcontainers:testcontainers-bom:1.16.0')
    compileOnly 'org.projectlombok:lombok'
//    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.3.Final'

    // Testing Dependencies
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation 'org.springframework.restdocs:spring-restdocs-webtestclient'
    testImplementation 'org.jeasy:easy-random-core:4.0.0'
    testImplementation 'org.mock-server:mockserver-netty:3.10.8'
    testImplementation 'org.mock-server:mockserver-client-java:3.10.8'

    // Integration Test Dependencies
//    integrationTestImplementation "de.flapdoodle.embed:de.flapdoodle.embed.mongo"
    // integrationTestImplementation "org.testcontainers:mongodb"
    // integrationTestImplementation 'org.testcontainers:localstack'
    // integrationTestImplementation "org.testcontainers:junit-jupiter"
}

tasks.named('test') {
    outputs.dir snippetsDir
    useJUnitPlatform()
} 

// tasks.named('integrationTest') {
//     outputs.dir snippetsDir
//     useJUnitPlatform()
// }

tasks.named('asciidoctor') {
    inputs.dir snippetsDir
    dependsOn test
}

task generateGitProperties {
    doLast {
        def gitProps = file("${projectDir}/src/main/resources/git.properties")
        gitProps.text = """
        git.branch=${'git rev-parse --abbrev-ref HEAD'.execute().text.trim()}
        git.commit.id=${'git rev-parse --short HEAD'.execute().text.trim()}
        git.commit.time=${'git log -1 --format=%cI'.execute().text.trim()}
        """.stripIndent()
    }
}

openApiGenerate {
    generatorName = "spring"
    inputSpec = 'ClashBot-OpenAPI/definitions/open-api.yml'.toString()
    outputDir = "$buildDir/generated".toString()
    apiPackage = "com.poss.clash.bot.openapi.api".toString()
    modelPackage = "com.poss.clash.bot.openapi.model".toString()
    configOptions = [
            dateLibrary: "java8",
            reactive: "true",
            interfaceOnly: "true",
            skipDefaultInterface: "true",
            additionalModelTypeAnnotations: "@lombok.Data();@lombok.NoArgsConstructor();@lombok.AllArgsConstructor();@lombok.Builder()",
            useSwaggerUI: "true",
            useJakartaEe: "true"
    ]
}


repositories {
    mavenCentral()
}

compileJava.dependsOn tasks.openApiGenerate
processResources.dependsOn generateGitProperties